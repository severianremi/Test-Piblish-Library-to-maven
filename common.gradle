apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.severianremi'
version = "1.0.3"

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->

        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }

        def flavorName = variant.flavorName.toLowerCase()
        def flavorSourcesTask = tasks.create(name: "${flavorName}SourcesJar", type: Jar) {
            archiveClassifier.set("${flavorName}-sources")
            from android.sourceSets.main.javaDirectories
            from android.sourceSets[flavorName].javaDirectories
        }

        artifacts {
            archives flavorSourcesTask
        }

    }
}
